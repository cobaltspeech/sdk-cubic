apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf' // Added to let us generate the use the protobuf section below.

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.cobaltspeech.cubicdemo"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

// This section adds a step to generate the gRPC code from `app/src/main/proto` proto files.
protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.10.0'
    }
    plugins {
        javalite {
            artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
        }
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.24.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite {}
                grpc {
                    // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}


dependencies {
    implementation 'com.android.support:appcompat-v7:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    // Libraries needed for grpc stuff. (changing the versions may require different libraries.)
    implementation 'io.grpc:grpc-okhttp:1.24.0'
    implementation 'io.grpc:grpc-protobuf-lite:1.24.0'
    implementation 'io.grpc:grpc-stub:1.24.0'
    implementation 'io.grpc:grpc-auth:1.24.0'
    implementation 'javax.annotation:javax.annotation-api:1.2'
}
